apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        lintOptions {
            abortOnError false
            checkReleaseBuilds false
            quiet true
            ignoreWarnings true
        }
    }

    signingConfigs {
        release {
            storeFile file('../keystore/JiongBull.jks')
            Properties properties = new Properties()
            properties.load(rootProject.file('local.properties').newDataInputStream())
            def keystorePass = properties.getProperty('keystorePass')
            def aliasName = properties.getProperty('aliasName')
            def aliasPass = properties.getProperty('aliasPass')

            storePassword keystorePass != null ? keystorePass : System.getenv('keystorePass')
            keyAlias aliasName != null ? aliasName : System.getenv("aliasName")
            keyPassword aliasPass != null ? aliasPass : System.getenv("aliasPass")
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
        }
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    output.outputFile = new File(
                            output.outputFile.parent + "/${variant.buildType.name}",
                            "jlog_sample_${variant.buildType.name}.apk".toLowerCase())
                }
            }
        }
    }
}

dependencies {
    def sampleModule = rootProject.ext.sampleModule

    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile sampleModule.support
    compile project(sampleModule.library)
}